def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

//apply plugin: 'com.github.dcendents.android-maven'

group='com.github.cleaninsights'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 3
def myVersionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
def myVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

ext {
    bintrayRepo = 'maven'
    bintrayName = 'cleaninsights-piwik-sdk-android'

    publishedGroupId = 'io.cleaninsights.sdk'
    libraryName = 'Clean Insights Piwik SDK for Android'
    artifact = 'cleaninsights-sdk'

    libraryDescription = 'Clean Insights SDK for Android based on Piwik '

    siteUrl = 'https://cleaninsights.org'
    gitUrl = 'https://gitlab.com/cleaninsights/clean-insights-android-sdk.git'

    libraryVersion = myVersionName

    developerId = 'cleaninsights'
    developerName = 'Clean Insights'
    developerEmail = 'support@guardianproject.info'

    licenseName = 'TBD'
    licenseUrl = 'https://gitlab.com/cleaninsights/clean-insights-android-sdk/-/blob/master/LICENSE'
    allLicenses = ["TBD"]
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode myVersionCode
        versionName myVersionName
    }

}

dependencies {
    repositories {
        jcenter()
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.annotation:annotation:1.1.0'

    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation group: 'com.google.guava', name: 'guava', version: '24.1-jre'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    //GuardianProject NetCipher to support network obfuscation, and strong TLS configuration
    //compile 'info.guardianproject.netcipher:netcipher:2.0.0-alpha1'

    //Supporting pinning!
    implementation 'com.squareup.okhttp3:okhttp:3.14.0'

    //network hardening and onion routing
    implementation 'info.guardianproject.netcipher:netcipher:2.1.0'
    implementation 'info.guardianproject.netcipher:netcipher-okhttp3:2.0.0-alpha1'

    //SquareUp Tape library, for QueueFile
    implementation 'com.squareup.tape2:tape:2.0.0-SNAPSHOT'

    //ui
    implementation 'com.tapadoo.android:alerter:5.1.2'

    // Espresso
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0')
    androidTestImplementation('com.android.support.test:testing-support-lib:0.1')
    // Robolectric
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.hamcrest:hamcrest-integration:1.3'
    testImplementation('org.robolectric:robolectric:3.3.2') {
        exclude module: 'classworlds'
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-provider-api'
    }
}

/**
 * JAR
 */
task clearJar(type: Delete) {
    delete fileTree(dir: "jar", exclude: "dont_delete.txt")
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('jar/')
    include('classes.jar')
    rename('classes.jar', 'CleanInsightsPiwikAndroidSdk-' + gitSha() + '.jar')
}

makeJar.dependsOn(clearJar, build)

/**
 * Coverage
 */
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

jacoco {
    toolVersion = "0.7.1.201405082137"
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0x'
    }
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['org/piwik/R*.class',
                       '**/BuildConfig.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class'
            ])
    sourceDirectories = files([
            'src/main/java',
    ])
    executionData = files('build/jacoco/testDebugUnitTest.exec')
}

coveralls {
    jacocoReportPath = 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
    sourceDirs = ['src/main/java']
}

//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
